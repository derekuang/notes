---
tags: Erlang OTP
---

# 总览

## 0、定义

OTP设计原则定义了如何构建Erlang代码中的过程，模块和目录条款。

[OTP设计原则官方文档](http://erlang.org/doc/design_principles/des_princ.html)

[OTP设计原则腾讯云文档](https://cloud.tencent.com/developer/chapter/12362)



## 1、监控树（Supervision Trees）

- Erlang/OTP中的基本概念是**监控树**。这是一个基于**工人**和**主管**理念的流程构建模型。
  - 工人是执行计算的进程。
  - 主管是监督工人行为的进程，出现问题时可重启工人。
  - 监控树是代码到主管和工人的分级安排，这使得设计和编程容错软件成为可能。



## 2、行为（Behaviours）

- 在监督树中，许多进程具有相似的结构，它们遵循类似的模式。例如，主管的结构相似。他们之间唯一的区别是他们监督哪个子进程。许多工作者都是服务器 - 客户端关系中的服务器，有限状态机器或错误记录器等事件处理程序。

- **行为**是这些常见模式的形式化。这个想法是在通用部分（行为模块）和特定部分（**回调模块**）中划分进程的代码。

- 行为模块是 Erlang / OTP 的一部分。为了实现诸如监督员之类的过程，用户只需实现回调模块，该回调模块将导出预先定义的一组功能，即**回调功能**。

- 以下示例说明如何把代码分为通用部分和特定部分：

  ```
  -module(ch1).
  -export([start/0]).
  -export([alloc/0, free/1]).
  -export([init/0]).
  
  start() ->
      spawn(ch1, init, []).
  
  alloc() ->
      ch1 ! {self(), alloc},
      receive
          {ch1, Res} ->
              Res
      end.
  
  free(Ch) ->
      ch1 ! {free, Ch},
      ok.
  
  init() ->
      register(ch1, self()),
      Chs = channels(),
      loop(Chs).
  
  loop(Chs) ->
      receive
          {From, alloc} ->
              {Ch, Chs2} = alloc(Chs),
              From ! {ch1, Ch},
              loop(Chs2);
          {free, Ch} ->
              Chs2 = free(Ch, Chs),
              loop(Chs2)
      end.
  ```

  （alloc/0、free/1相当于通用部分，消息接收后的内容相当于特定部分）。

- 服务器的代码可以重写为通用部分`server.erl`和一个回调模块`ch2.erl`：
  `server.erl`（可扩展该模块功能而不用修改回调模块）：

  ```
  -module(server).
  -export([start/1]).
  -export([call/2, cast/2]).
  -export([init/1]).
  
  start(Mod) ->
      spawn(server, init, [Mod]).
  
  call(Name, Req) ->
      Name ! {call, self(), Req},
      receive
          {Name, Res} ->
              Res
      end.
  
  cast(Name, Req) ->
      Name ! {cast, Req},
      ok.
  
  init(Mod) ->
      register(Mod, self()),
      State = Mod:init(),
      loop(Mod, State).
  
  loop(Mod, State) ->
      receive
          {call, From, Req} ->
              {Res, State2} = Mod:handle_call(Req, State),
              From ! {Mod, Res},
              loop(Mod, State2);
          {cast, Req} ->
              State2 = Mod:handle_cast(Req, State),
              loop(Mod, State2)
      end.
  ```

  `ch2.erl`：

  ```
  -module(ch2).
  -export([start/0]).
  -export([alloc/0, free/1]).
  -export([init/0, handle_call/2, handle_cast/2]).
  
  start() ->
      server:start(ch2).
  
  alloc() ->
      server:call(ch2, alloc).
  
  free(Ch) ->
      server:cast(ch2, {free, Ch}).
  
  init() ->
      channels().
  
  handle_call(alloc, Chs) ->
      alloc(Chs). % => {Ch,Chs2}
  
  handle_cast({free, Ch}, Chs) ->
      free(Ch, Chs). % => Chs2
  ```

  start/0、alloc/0、free/1是暴露给外部的接口（接口函数）。

- 不使用行为编写的代码可以更加高效，但作为代价，要牺牲通用性。以一致的方式管理系统中所有应用程序的能力非常重要。

- 标准的Erlang/OTP行为有：

  - gen_server：用于实现客户端 - 服务器关系的服务器
  - gen_statem：用于实现状态机
  - gen_event：用于实现事件处理功能
  - supervisor：用于实现监控树中的监督者



## 3、应用（Applications）

- Erlang / OTP 带有许多组件，每个组件都实现了一些特定的功能。在Erlang / OTP 术语中这些组件被称为**应用程序(Application)** 。如Mnesia、Debugger等。
- 基于 Erlang / OTP 的最小系统由以下两个应用程序组成：
  - kernel：运行Erlang必须的功能
  - STDLIB：Erlang标准库
- 应用程序概念同时适用于程序结构（进程）和目录结构（模块）。
- 最简单的应用程序没有任何进程，但由一组功能模块组成。这样的应用程序被称为**库应用**。库应用程序的一个示例是 STDLIB。
- 拥有进程的应用程序最容易实现为使用标准行为的监控树。



## 4、发布（Releases）

- **发行版本**是由 Erlang/ OTP 应用程序的子集和一组用户特定的应用程序所开发的完整的系统。



## 5、版本处理（Release Handling）

- **发布处理**是在（可能）正在运行的系统中升级和降级发行版的不同版本。

